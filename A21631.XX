	.TITLE	SUPER
	.INCLUDE PINBOL
	.SBTTL*************************
	.SBTTL*
	.SBTTL* PROGRAMMER:RICH MOORE & NORMAN AVELLAR
	.SBTTL*
	.SBTTL* CHECK:
	.SBTTL*
	.SBTTL*************************
;
;DATE INITATED: 7/18/78
;PROJECT CHARGE #: 6Y945
;DISK #: 	52
;HARDWARE REQUIREMENTS:
; SUPER PIN PINBALL HARDWARE
;
;
;
;MEMORY REQUIREMENTS:
; GAME ROM: 6800-6FFF (2K)
;
;
;
;INTERRUPT REQUIREMENTS:
; OPERATING SYSTEM-PINBOL
;
;
;
;ASSEMBY COMMAND STRING:
; DK1:SUPER,DK1:SUPER=DX1:PINBOL,DX1:SUPER/C:S:E
;
;
;
;LINK COMMAND STRING:
;3M:/L,DK1:RICH.LST/A=DK1:SUPER,DX1:PINTER,PINCON,PINTST,PINBL3
;
;
;
;PROGRAM DESCRIPTION:
; SUPER PIN. ULTRA LARGE 1 TO 4 PLAYER
; PINBALL W/MT. EVEREST MOTF.
;
;
;

	.SBTTL	SUPER PIN PINBALL GAME
;
;EXTERNAL LINKAGES
;
	.GLOBL	UVECTOR		;SWITCH VECTORS
	.GLOBL	SAVSTATE	;INTERBALL STATE SAVE
	.GLOBL	SOLSTATE	;SOLENOID TIMING
	.GLOBL	REPSND		;REPLAY SOUND POINTER
	.GLOBL	SNDDES		;SOUND TABLES
	.GLOBL	PMATCH		;MATCH TABLE POINTER
	.GLOBL	FCOINS		;COIN OPTIONS
	.GLOBL	UMAXBL		;MAX BALLS
	.GLOBL	UMAXPL		;MAX PLAYERS
	.GLOBL	USTAT		;STATISTICS ENTRY
	.GLOBL	USTDISP		;STATISTICS DISPLAY
;
;NAMING SECTION
;...SWITCHES...
;
	SWITCH	STRTSW,63	;START SWITCH
	SWITCH	TILTSW,48	;TILT SW
	SWITCH	ATTRSW,14	;ATTRACT SW
	SWITCH	RTHSW,52	;RIGHT THUMP SW
	SWITCH	LTHSW,53	;LEFT THUMP SW
	SWITCH	RSLSW,56	;RIGHT SLING SW
	SWITCH	LSLSW,57	;LEFT SLING SW
	SWITCH	R50SW,13	;RIGHT 50 PT. SW
	SWITCH	OHOLSW,1	;OUT HOLE SW
	SWITCH	RSENCSW,12	;RIGHT SENCE SW
	SWITCH	LN1DBSW,18	;LANE 1 (DOUBLE) SW
	SWITCH	LN2EXSW,19	;LANE 2 (EXTRA) SW
	SWITCH	LN3SPSW,20	;LANE 3 (SPECIAL) SW
	SWITCH	DBLNSW,21	;DOUBLE LANE SW
	SWITCH	EXLNSW,24	;EXTRA LANE SW
	SWITCH	SPLNSW,25	;SPECIAL LANE SW
	SWITCH	RCIRSW,26	;RIGHT CIR DET SW
	SWITCH	LLNSW,28	;LEFT LANE SW
	SWITCH	LCLNSW,29	;LEFT CNTR LANE SW
	SWITCH	RCLNSW,32	;RIGHT CNTR LANE SW
	SWITCH	RLNSW,33	;RIGHT LANE SW
	SWITCH	LLTRGSW,34	;LEFT LOWER TARGET SW
	SWITCH	LTRGSW,35	;LEFT TARGET SW
	SWITCH	RTRGSW,36	;RIGHT TARGET SW
	SWITCH	RLTRGSW,37	;RIGHT LOWER TARGET SW
	SWITCH	SPINSW,40	;SPINNER SW
	SWITCH	FLGSW,62	;OS HAS SET A FLAG SW
	SWITCH	FLASHSW,15	;FLASHER TASK SW
	SWITCH	DOBSW,22	;DOUBLE BONUS AWARD SWITCH
	SWITCH	EXTRSW,23	;EXTRA BALL AWARD SWITCH
	SWITCH	SPECSW,30	;SPECIAL AWARD SWITCH
	SWITCH	LMPSW,31	;LAMP FLASHER TASK SWITCH
	SWITCH	CTTRSW,39	;'C' ATTRACT TASK SWITCH
	SWITCH	DTTRSW,46	;'D' ATTRACT TASK SWITCH
	SWITCH	ETTRSW,47	;'E' ATTRACT TASK SWITCH
	SWITCH	XBLSW,55	;EXTRA BALL (SPSL) FLASH
;
;...OPTION SWITCHES...
;
	SWITCH	ADABALL,68	;ADD-A-BALL (SPECIAL)
	SWITCH	SPECALT,70	;SPECIAL ALTERNATE
	SWITCH	NOXBALL,77	;NO EXTRA BALL
	SWITCH	XBLOPT,78	;EXTRA BALL PTS OPTION
	SWITCH	MAXBALL,67	;MAX BALL
	SWITCH	FTOPSW,93	;LADDER FORGET AT TOP
	SWITCH	LNOPSW,85	;NO LANE LAMPS
	SWITCH	ULNFSW,76	;UPPER LANE FEATURE AWARD
	SWITCH	LLNFSW,66	;LOWER LANE FORGET
	SWITCH	FTLNSW,79	;ALTER OR BOTH LANE LAMPS
;
;...LAMPS...
;
	LAMP	B1LMP,0		;BONUS LAMPS
	LAMP	B2LMP,1
	LAMP	B3LMP,2
	LAMP	B4LMP,3
	LAMP	B5LMP,4
	LAMP	B6LMP,5
	LAMP	B7LMP,6
	LAMP	B8LMP,7
	LAMP	B9LMP,8
	LAMP	B10LMP,9
	LAMP	B20LMP,10
	LAMP	B30LMP,11
	LAMP	B40LMP,12
	LAMP	DB1LMP,13	;DOUBLE BONUS LAMPS
	LAMP	DB2LMP,14
	LAMP	DB3LMP,15
	LAMP	DBLMP,16
	LAMP	DBLNLMP,17
	LAMP	EX1LMP,18	;EXTRA BALL LAMPS
	LAMP	EX2LMP,19
	LAMP	EX3LMP,20
	LAMP	EX4LMP,21
	LAMP	EXLMP,22
	LAMP	EXLNLMP,23
	LAMP	SP1LMP,24	;SPECIAL LAMPS
	LAMP	SP2LMP,25
	LAMP	SP3LMP,26
	LAMP	SP4LMP,27
	LAMP	SP5LMP,28
	LAMP	SPLMP,29
	LAMP	SPLNLMP,30
	LAMP	LLNLMP,31	;LEFT LANE LAMP
	LAMP	LCLNLMP,32	;LEFT CNTR LANE LAMP
	LAMP	RCLNLMP,33	;RIGHT CNTR LANE LAMP
	LAMP	RLNLMP,34	;RIGHT LANE LAMP
	LAMP	LLTRGLMP,35	;LOWER LEFT TARGET LAMP
	LAMP	LTRGLMP,36	;LEFT TRGT LAMP
	LAMP	RTRGLMP,37	;RIGHT TRGT LAMP
	LAMP	RLTRGLMP,38	;LOWER RIGHT TRGT LAMP
	LAMP	LTHLMP,39	;LEFT THUMPER LAMP
	LAMP	RTHLMP,40	;RIGHT THUMPER LAMP
	LAMP	SPINLMP,41	;SPINNER LAMP
	LAMP	SPSLMP,43	;SAME PLAYER SHOOTS
	LAMP	DOBLMP,44
	LAMP	XBLMP,45
	LAMP	SPIN1LMP,48	;1K SPINNER LAMP
	LAMP	SPIN2LMP,49	;2K SPINNER LAMP
	LAMP	SPECLMP,46
;
;... O/S LAMPS ...
;
	LAMP	HSTD,54
	LAMP	P1MIL,56
	LAMP	P2MIL,57
	LAMP	P3MIL,58
	LAMP	P4MIL,59
	LAMP	MACHLMP,60
	LAMP	HSMIL,61
	LAMP	BALLMP,62
	LAMP	GMOVER,63
	LAMP	TLTLMP,55	;TILT LAMP 
;
;...SOLENOIDS...
;
	SOLENOID LTHSOL,5	;LEFT THUMPER SOLENOID
	SOLENOID RTHSOL,4	;RIGHT THMPR SOL
	SOLENOID LSLSOL,9	;LFT SLINGSHOT
	SOLENOID RSLSOL,8	;RGHT SLINGSHOT
	SOLENOID OUTHSOL,15	;OUT HOLE
	SOLENOID FLIPSOL,16	;FLIPPER
	SOLENOID LTHEN,5+24	;LEFT THUMP ENABLE
	SOLENOID RTHEN,4+24	;RGHT THUMP ENABLE
	SOLENOID LSLEN,9+24	;LEFT SLING ENABLE
	SOLENOID RSLEN,8+24	;RGHT SLING ENABLE
;
;...FLAGS...
;
	FLAG	B10FLG,0	;BONUS 10K
	FLAG	B20FLG,1	;BONUS 20K
	FLAG	B30FLG,2	;BONUS 30K
	FLAG	B40FLG,3	;BONUS 40K
	FLAG	B1FLG,23	;BONUS FLAGS
	FLAG	B2FLG,24
	FLAG	B3FLG,25
	FLAG	B4FLG,26
	FLAG	B5FLG,27
	FLAG	B6FLG,28
	FLAG	B7FLG,29
	FLAG	B8FLG,30
	FLAG	B9FLG,31
	FLAG	FLPFLG,4	;FEATURE LOOP FLAG
	FLAG	BFFLG,16		;BONUS FLASH FLAG
	FLAG	TLTDFLG,6	;TILT DEBOUNCE
	FLAG	RSLFLG,21	;RIGHT SLINGSHOT
	FLAG	LSLFLG,22	;LEFT SLINGSHOT
	FLAG	LFFLG,33		;FLASHER FLAG
	FLAG	OUTFLG,34	;OUTHOLE FLAG
	FLAG	BONFLG,15	;BONUS COLLECTING FLAG
	FLAG	DLYFLG,32	;FEATURE LOOP DELAY
	FLAG	TLT2FLG,7	;TILT FLAG
	FLAG	DBLNFLG,18	;DOUBLE LANE FLAG
	FLAG	EXLNFLG,19	;EXTRA BALL LANE FLAG
	FLAG	SPLNFLG,20	;SPECIAL LANE FLAG
	FLAG	BALLFLG,35	;ADD-A-BALL FLAG
	FLAG	FLFLG,45	;FEATURE LOOP FLASH
	FLAG	SPCFLG,8
	FLAG	EXFLG,12
	FLAG	DBFLG,17
;
;...LOCKOUT FLAGS...
;
	FLAG	LANLKF,5	;LANE LOCKOUT FLAG
	FLAG	EXBLKF,9	;XBALL LOCKOUT
	FLAG	CRLNLKF,10	;CIRCLE LANE ROLLOVER LOCKOUT
	FLAG	CIRLKF,11	;CIRCLE SENSE ROLLOVERS
	FLAG	DOBLKF,13	;DOUBLE BONUS LOCKOUT
	FLAG	SPLKF,14	;SPECIAL LOCKOUT
;
;OPERATING SYSTEM/PINBOL COMMUNICATION FLAGS
;
	FLAG	RSTRTFLG,55	;RESTART FLAG
	FLAG	TLTFLG,63	;TILTED FLAG
	FLAG	ATRFLG,62	;ATTRACT *
	FLAG	SCRFLG,59	;PLAYER HAS SCORED *
	FLAG	XBLFLG,58	;XTRA BALL AWARDED *
	FLAG	LASFLG,61	;LAST BALL IN PLAY *
	FLAG	RE1FLG,56	;AWARD 1 REPLAY
	FLAG	RE2FLG,57	;AWARD 2 REPLAY
	FLAG	NXPFLG,60	;NEXT PLAYER UP *
; * INDICATES O.S. SETS SWITCH (FLGSW) FOR PROCESSING FLAGS
;
;...COUNTERS AND METERS...
;
	COUNTER CNTR,0
	COUNTER CNT1,2
	COUNTER CNT2,4
	COUNTER CNT3,6
	COUNTER CNT4,8
	COUNTER	CNT5,10
	COUNTER	CNT6,12
	COUNTER	CNTRA,14
;
;...SOUNDS...
;
	SND	TLTSND,0	;TILT
	SND	DOBSND,11	;DOUBLE SOUND
	SND	EXSND,5		;XTRA BALL SOUND
	SND	SPSND,4		;SPECIAL SOUND
	SND	BONSND,12	;BONUS SOUND
	SND	THOU,16		;1K SOUND
	SND	HUND,18		;100 SOUND
	SND	TEN,19		;10 SOUND
	SND	CREDIT,1	;CREDIT
	SND	SPR50,7		;50K SOUND
	SND	LROSND,8	;LOWER ROLLOVER SOUND
	SND	FLTSND,9	;FEATURE LOOP TRANSMITTER
	SND	FLRSND,10	;FEATURE LOOP RECEIVER SOUND
	SND	SPN1K,15	;SPINNER 1K
	SND	SPN100,17	;SPINNER 100
	SND	MATCH,3		;MATCH SOUND
	SND	SPSND1,6	;SPECIAL SOUND #1
	SND	BDRSND,13	;BONUS DRAIN
	SND	FLASND,14	;FEATURE LOOP ADV SOUND
	SND	LSTSND,20	;LOWEST PRIORITY
;
;...METERS IN BATTERY BACK UP...
;
	COUNTER	SPMTR,42+16	;SPECIAL METER
	COUNTER	RPMTR,30+16	;REPLAY METER
	COUNTER	XBMTR,48+16	;XTRA BALL METER
	COUNTER	TLMTR,54+16	;TILT METER
	COUNTER	TPMTR,14+16	;TOTAL PLAYS METER
;
;...VECTORS...
;
;SWITCH VECTORS
UVECTOR:
	STRVEC	NOSWITCH
	VECTOR	STRTSW,START	;START PLAYER
	VECTOR	TILTSW,TILT	;TILTED
	VECTOR	RTHSW,RTH	;RIGHT THUMPER
	VECTOR	LTHSW,LTH	;LEFT THUMPER
	VECTOR	RSLSW,RSL	;RIGHT SLINGSHOT
	VECTOR	LSLSW,LSL	;LEFT SLINGSHOT
	VECTOR	R50SW,R50	;RIGHT 50PT
	VECTOR	OHOLSW,OHOL	;OUTHOLE
	VECTOR	RSENCSW,RSENC	;RIGHT SENSE
	VECTOR	LN1DBSW,LN1	;LANE 1 (DOUBLE)
	VECTOR	LN2EXSW,LN2	;LANE 2 (EXTRA)
	VECTOR	LN3SPSW,LN3	;LANE 3 (SPECIAL)
	VECTOR	DBLNSW,DBLN	;DOUBLE LANE
	VECTOR	EXLNSW,EXLN	;EXTRA LANE
	VECTOR	SPLNSW,SPLN	;SPECIAL LANE
	VECTOR	RCIRSW,RCIR	;RIGHT CIRCLE DET.
	VECTOR	LLNSW,LLN	;LEFT LANE
	VECTOR	LCLNSW,LCLN	;LEFT CNTR LANE
	VECTOR	RCLNSW,RCLN	;RGHT CNTR LANE
	VECTOR	RLNSW,RLN	;RIGHT LANE
	VECTOR	LLTRGSW,LLTRG	;LEFT LOWER TARG.
	VECTOR	LTRGSW,LTRG	;LEFT TARG.
	VECTOR	RTRGSW,RTRG	;RIGHT TARG.
	VECTOR	RLTRGSW,RLTRG	;RIGHT LOWER TARG.
	VECTOR	SPINSW,SPIN	;SPINNER
	VECTOR	FLGSW,OSLINK	;OS FLAG
	VECTOR	FLASHSW,FLASH	;FLASHING LIGHT ROUTINE
	VECTOR	DOBSW,DOUBLE	;DOUBLE BONUS
	VECTOR	EXTRSW,EXTRA	;EXTRA BALL
	VECTOR	SPECSW,SPECIAL	;SPECIAL
	VECTOR	LMPSW,LAMPFL	;LAMP FLASH
	VECTOR	CTTRSW,CTTASK	;ATTRACT TRG,SPIN  4-STATE
	VECTOR	DTTRSW,DTTASK	;ATTRACT 10-25 BON  2-STATE
	VECTOR	ETTRSW,ETTASK	;ATTRACT LN  2-STATE
	VECTOR	XBLSW,SPFLASH	;SPSLMP FLASH
	ENDVEC

	.SBTTL	SYSTEM PARAMETERS
;
;SYSTEM PARAMETER SECTION
;
;...STATE SAVED BETWEEN BALLS
;
SAVSTATE:
	LITESTATE DB1LMP DB2LMP DB3LMP DBLMP,B1LMP,B2LMP,B3LMP,B4LMP,B5LMP,B6LMP,B7LMP
	LITESTATE B8LMP,B9LMP,B10LMP,B20LMP,B30LMP,B40LMP
	LITESTATE EX1LMP EX2LMP EX3LMP EX4LMP EXLMP SP1LMP SP2LMP SP3LMP SP4LMP SP5LMP SPLMP
	LITESTATE DBLNLMP EXLNLMP SPLNLMP SPINLMP SPIN1LMP SPIN2LMP
	LITESTATE LLNLMP,LCLNLMP,RCLNLMP,RLNLMP
	FLAGSTATE DBLNFLG EXLNFLG SPLNFLG B10FLG B20FLG B30FLG B40FLG B1FLG B2FLG B3FLG B4FLG
	FLAGSTATE B5FLG,B6FLG,B7FLG,B8FLG,B9FLG,SPCFLG,EXFLG,DBFLG
;
;...SOLENOID TIMING AND INTERRUPTABILITY
;
SOLSTATE:
	SOLTAB	32		;3 MIDDLE COIN COUNTER
	SOLTAB	40		;4 RIGHT THUMPER
	SOLTAB	40		;5 LEFT THUMPER
	SOLTAB	32		;6 LEFT COIN COUNTER
	SOLTAB	32		;7 RIGHT COIN COUNTER
	SOLTAB	72		;8 RIGHT SLINGSHOT
	SOLTAB	72		;9 LEFT SLINGSHOT
	SOLTAB	32		;10
	SOLTAB	32		;11
	SOLTAB	32		;12
	SOLTAB	32		;13
	SOLTAB	32		;14 TOTAL PLAYS METER
	SOLTAB	80		;15 OUTHOLE
	.RADIX 16
FCOINS:	.BYTE 12,14,16,18,1A,1C,1E,50,60,70,0C0,80	;COIN OPTIONS TABLE
	.BYTE 0D0,0E0,90,0F0
;
USTAT:	.BYTE	39,0,0		;TEST VECTOR
;
USTDISP: .BYTE	39,0,0		;SELF TEST VECTOR
;
	.RADIX 10
;
;POINTER TO MATCH TABLE
;
PMATCH: .BYTE 24		;INITIAL DELAY
	.BYTE 1			;DELTA DELAY
	.WORD 15		;MATCH LOOP CNTR
;
UMAXBL:	.BYTE 3,5		;MAX BALLS
UMAXPL:	.BYTE 4			;MAX PLAYERS
;
;POINTER TO REPLAY SOUND
;
REPSND:	.BYTE	3		;SOUND 4 REPEATED 3 TIMES
;
;SOUND DESCRIPTORS
;
SNDDES:	SNDTAB	TILTSN,0DD,27,27,0	;TILT 0
	SNDTAB	CREDSN,4F,16,21,0	;CREDIT 1
	SNDTAB	STRTSN,45,22,22,0	;START 2
	SNDTAB	MTCHSN,56,8,8,0		;MATCH 3
	SNDTAB	SPECSN,5A,15,15,0	;SPECIAL 4
	SNDTAB	XBALSN,44,15,15,0	;XBALL SOUND 5
	SNDTAB	SPCISN,49,12,12,5000	;SPECIAL #1 6
	SNDTAB	SP50SN,0C0,16,24,2500	;25K POINT SOUND 7
	SNDTAB	LROSN,5B,11,23,500	;LOWER ROLLOVER 8
	SNDTAB	FLTSN,0D0,15,27,100	;FL TRANS 9
	SNDTAB	FLRSN,4E,15,27,100	;FL REC 10
	SNDTAB	DOUBSN,53,20,20,0	;DOUBLE BONUS SOUND 11
	SNDTAB	BONSN,42,13,23,0	;BONUS 12
	SNDTAB	BDRNSN,4F,17,27,1000	;BONUS DRAIN SOUND 13
	SNDTAB	FLRSN,44,15,27,0	;FEATURE LOOP SOUND 14
	SNDTAB	THOUSN,4C,3,15,1000	;1K SPINNER SOUND 15
	SNDTAB	THOUSN,4C,15,15,1000	;1K 16
	SNDTAB	SPINSN,4F,10,23,100	;100 PTS SPINNER 17
	SNDTAB	S100SN,4F,10,23,100	;100 PTS 18
	SNDTAB	S10SN,5B,6,14,10	;10 PTS 19
	.RADIX	16
S10SN:
THOUSN:
	.BYTE	0A,0A,1A,1A,3A,3A,1B,1C,0C,0E,0E
	.BYTE	1E,4E,7E,8F
TILTSN:	.BYTE	0BE,0AE,8E,6E,4E,2E,2E,2E,4E,4E,4E,5E
	.BYTE	5E,5E,5D,6D,6C,6C,7B,7B,8A,9A,0A9,0B9
	.BYTE	0B8,0C8,0C7
STRTSN:
CREDSN:	.BYTE	0E,0E,1E,1D,2D,2C,3B,3B,4A,4A,4A,59,58
	.BYTE	58,58,98,98,98,0B8,0B8,0B8
FLRSN:
XBALSN:
SPECSN:	.BYTE	0FE,0EE,0DE,0CD,0BD,0AD,9C,8C,7C,6B,5B
	.BYTE	4B,3A,2A,1A
SPINSN:
BONSN:
MTCHSN:
FLTSN:
SPCISN:
SP50SN:
S100SN:	.BYTE	8F,0AF,0BF,0CF,0DF,0AF,8F,6F,5F,5F,5F,5F,4F
	.BYTE	4F,4F,3F,3F,3F,3F,2F,2F,2F,2F
LROSN:	.BYTE	0C7,45,4E,5F,6B,6F,6E,6B,6A,7C,9F,86,8F,8E
	.BYTE	88,0D8,0C5,0C5,0B9,0AA,9A,7A,69,68,27,36,25
BDRNSN:
	.BYTE	5F,5F,5F,5F,5F,5F,14,14,14,14,14,0AF,9F,6E,8D,5C,6B,3A,49
	.BYTE	18,37,6,2F,0F,1F,0F,0F
DOUBSN:	.BYTE	69,5D,9D,7F,9F,89,0AF,9A,0BF,0AB,0CF,0AC,0BF
	.BYTE	9E
	.RADIX	10
	.NLIST	ME
	.PAGE
;
;VECTOR PROCESSING
;
NOSWITCH:BREAK			;ERROR UNDEFIED SWITCH

	.SBTTL	TILT ROUTINE
TILT:   LOCKOUT	TLTDFLG
	INCR	TLMTR		;COUNT TILTS
TLTLOOP:SOUND	10 TLTSND	;OUT TO LUNCH !!!!!!!
	TURN	TLTLMP ON
	CLRALL	FLIPSOL,LTHEN,RTHEN,LSLEN,RSLEN
TLTWAIT:DELAY	1000
	IF	TILTSW GO TLTLOOP
	IF	ATRFLG GO 10$	;DONT WAIT FOR OUTHOLE
	IF	NOT OHOLSW GO TLTWAIT
10$:	TURN	TLTLMP OFF	;CLEAR TILT LAMP
	TURN	TLTDFLG OFF
	TURN	TLTFLG OFF	;ALLOW TILTS
	IF	ATRFLG GO ATTRACT
	TURN	TLT2FLG ON	;TILT FLAG FOR OHOL
	TURN	OUTFLG OFF	;ALLOW OUTHOLE ROUTINE
	TURN	FLASHSW ON
	IF	SCRFLG GO OHOL
	TURN	EXBLKF OFF
	GO	OHOL
	.PAGE
	.SBTTL	OS HAS SET A FLAG
;
;CLEAR XBALL AND SPECIAL ON FIRST SCORE
;
OSLINK:	IF	NOT SCRFLG GO 10$
	TURN	EXBLKF OFF
10$:	IF	NOT XBLFLG GO 20$
	DO	EXTRA
	TURN	XBLFLG OFF
20$:	IF	NOT ATRFLG DONE
	.SBTTL	ATTRACT MODE
;
;
ATTRACT:TURN	FLIPSOL OFF
	TURN	CTTRSW ON
	TURN	ETTRSW ON
	TURN	DTTRSW ON
ADFLSH:	LAMPMODE 128
	LOFF	SPLMP
	LON	DB1LMP,DEL
	LON	DB2LMP
	LON	EX1LMP,DEL
	LOFF	DB1LMP
	LON	DB3LMP
	LON	EX2LMP
	LON	SP1LMP,DEL
	LOFF	DB2LMP
	LOFF	EX1LMP
	LON	DBLMP
	LON	EX3LMP
	LON	SP2LMP,DEL
	LOFF	DB3LMP
	LOFF	EX2LMP
	LOFF	SP1LMP
	LON	EX4LMP
	LON	SP3LMP,DEL
	LOFF	DBLMP
	LOFF	EX3LMP
	LOFF	SP2LMP
	LON	EXLMP
	LON	SP4LMP,DEL
	LOFF	EX4LMP
	LOFF	SP3LMP
	LON	SP5LMP,DEL
	LOFF	EXLMP
	LOFF	SP4LMP
	LON	SPLMP,DEL
	LOFF	SP5LMP,DEL,ENDLM
	IF	FLFLG DONE
	GO	ADFLSH
;
CTTASK:	LAMPMODE 288
	LOFF	LLNLMP
	LOFF	RLNLMP
	LON	LCLNLMP
	LON	RCLNLMP
	LON	LLTRGLMP
	LOFF	RLTRGLMP,DEL
	LOFF	LCLNLMP
	LOFF	RCLNLMP
	LON	LLNLMP
	LON	RLNLMP
	LON	LTRGLMP
	LOFF	LLTRGLMP,DEL
	LOFF	LLNLMP
	LOFF	RLNLMP
	LON	LCLNLMP
	LON	RCLNLMP
	LON	RTRGLMP
	LOFF	LTRGLMP,DEL
	LOFF	LCLNLMP
	LOFF	RCLNLMP
	LON	LLNLMP
	LON	RLNLMP
	LON	RLTRGLMP
	LOFF	RTRGLMP,DEL,ENDLM
	GO	CTTASK
;
DTTASK:	SET	CNT6 B1LMP
10$:	TURN	WITH CNT6 ON
	DELAY	80
	INCR	CNT6
	IF	NOT EQUAL CNT6 B40LMP+1 GO 10$
	SET	CNT6 B1LMP
20$:	TURN	WITH CNT6 OFF
	DELAY	80
	INCR	CNT6
	IF	NOT EQUAL CNT6 B40LMP+1 GO 20$
	TURN	RSTRTFLG OFF	;PROTECTION AGAINST RESTARTS
	GO	DTTASK
;
ETTASK:	LAMPMODE 200
	LON	DBLNLMP
	LON	DOBLMP
	LON	SPIN2LMP
	LOFF	SPIN1LMP
	LOFF	SPECLMP
	LOFF	SPLNLMP,DEL
	LON	EXLNLMP
	LON	XBLMP
	LON	SPINLMP
	LOFF	SPIN2LMP
	LOFF	DOBLMP
	LOFF	DBLNLMP,DEL
	LON	SPLNLMP
	LON	SPECLMP
	LON	SPIN1LMP
	LOFF	SPINLMP
	LOFF	XBLMP
	LOFF	EXLNLMP,DEL,ENDLM
	GO	ETTASK
	.PAGE
	.SBTTL	START BALL SEQUENCE
START:	TURN	NXPFLG OFF
	TURN	FLASHSW ON	;FLASHER TASK
	TURN	RSTRTFLG OFF	;RESTART COMPLETED IF USED
	GO	SETSOL
	.PAGE
	.SBTTL	THUMPERS AND SLINGSHOTS
;RIGHT THUMPER
RTH:	DELAY	100
	TURN	RTHEN ON
	SCORE	HUND
	DONE
;LEFT THUMPER
LTH:	DELAY	100
	TURN	LTHEN ON
	SCORE	HUND
	DONE
;RIGHT SLINGSHOT
RSL:	LOCKOUT	RSLFLG
	SCORE	TEN
	DELAY	240.
	TURN	RSLEN ON	;ENABLE SLINGSHOT
	TURN	RSLFLG OFF
	GO	LANESP
;LEFT SLINGSHOT
LSL:	LOCKOUT	LSLFLG
	SCORE	TEN
	DELAY	240.
	TURN	LSLEN ON	;ENABLE SLINGSHOT
	TURN	LSLFLG OFF
;
LANESP:	IF	NOT FTLNSW DONE
	IF	NOT RCLNLMP GO 10$
	TURN	LCLNLMP ON
	TURN	RCLNLMP OFF
	GO	20$
10$:	IF	NOT LCLNLMP GO 20$
	TURN	RCLNLMP ON
	TURN	LCLNLMP OFF
20$:	IF	NOT LLNLMP GO 30$
	TURN	RLNLMP ON
	TURN	LLNLMP OFF
	DONE
30$:	IF	NOT RLNLMP DONE
	TURN	LLNLMP ON
	TURN	RLNLMP OFF
	DONE
	.PAGE
	.SBTTL	ROLLOVERS AND LANES
;LOWER ROLLOVER
RSENC:	SCORE	LROSND
	DONE
LN3:				;FEATURE LOOP START
LN2:
LN1:	LOCKOUT	CRLNLKF
LNLOOP:	SOUND	5 FLTSND
	DELAY	2000		;ALLOW TIME TO HIT RORL CIRSW
	TURN	CRLNLKF OFF
	DONE
;
;DOUBLE LANE ROLLOVER
;
DBLN:	LOCKOUT	LANLKF
	SCORE	3 THOU
	DO	BONADV
	TURN	DBLNFLG ON
	TURN	DBLNLMP ON
	SET	CNT2 DB1LMP
10$:	IF	NOT WITH CNT2 GO 20$
	IF	ULNFSW GO LNEX2
	INCR	CNT2
	IF	NOT EQUAL CNT2 DBLMP GO 10$
	GO	LNEX2
20$:	TURN	WITH CNT2 ON
	GO	LNEX3
;
;EXTRA-BALL LANE ROLLOVER
;
EXLN:	LOCKOUT	LANLKF
	SCORE	3 THOU
	DO	BONADV
	TURN	EXLNFLG ON
	TURN	EXLNLMP ON
	SET	CNT3 EX1LMP
10$:	IF	NOT WITH CNT3 GO 20$
	IF	ULNFSW GO LNEX2
	INCR	CNT3
	IF	NOT EQUAL CNT3 EXLMP GO 10$
	GO	LNEX2
20$:	TURN	WITH CNT3 ON
	GO	LNEX3
;
;SPECIAL LANE ROLLOVER
;
SPLN:	LOCKOUT	LANLKF
	SCORE	3 THOU
	DO	BONADV
	TURN	SPLNFLG ON
	TURN	SPLNLMP ON
	SET	CNT5 SP1LMP
10$:	IF	NOT WITH CNT5 GO 20$
	IF	ULNFSW GO LNEX2
	INCR	CNT5
	IF	NOT EQUAL CNT5 SPLMP GO 10$
	GO	LNEX2
20$:	TURN	WITH CNT5 ON
LNEX3:	DO	FEATCK
LNEX2:	TURN	LANLKF OFF
LNEXIT:	IF	DBLNFLG AND EXLNFLG AND SPLNFLG GO 10$	;SPINNER INCREASE
	DONE
10$:	IF	SPIN1LMP GO 12$
	TURN	SPIN1LMP ON
	GO	18$
12$:	IF	SPIN2LMP GO 16$
	TURN	SPIN2LMP ON
	GO	18$
16$:	TURN	SPINLMP ON
18$:	TURN	DBLNFLG OFF
	TURN	EXLNFLG OFF
	TURN	SPLNFLG	OFF
20$:	DONE
SPERN2:	IF	SPLKF DONE
	LOCKOUT	SPCFLG
	IF	LNOPSW DONE
	IF	FTLNSW GO 10$
	TURN	RLNLMP ON
	TURN	LLNLMP ON
	DONE
10$:	IF	LLNLMP DONE
	TURN	RLNLMP ON
	DONE
;
;DOUBLE BONUS
;
DOUBLE:	LOCKOUT	DOBLKF		;ONLY ONCE PER BALL
	TURN	DBFLG OFF
	TURN	DLYFLG ON
	SOUND	10 DOBSND
	SET	CNT4 DBLMP
	TURN	LFFLG ON
	TURN	LMPSW ON
	DO	DBFLSH
	DO	DBFLSH
	DO	DBFLSH
	TURN	LFFLG OFF
	TURN	DBLNLMP OFF
	TURN	DBLNFLG OFF
	TURN	DB1LMP ON
	TURN	DB2LMP ON
	TURN	DBLMP ON
FETEX:	DO	SPINBACK
	TURN	DLYFLG OFF
	DONE
DBFLSH:	LAMPMODE 150
	LON	DB1LMP
	LOFF	DB2LMP
	LOFF	DB3LMP,DEL
	LOFF	DB1LMP
	LON	DB2LMP,DEL
	LOFF	DB2LMP
	LON	DB3LMP,DEL,ENDLM
	DONE
;
;EXTRA BALL
;
EXTRA:	LOCKOUT	EXBLKF
	TURN	EXFLG OFF
	TURN	DLYFLG ON
	TURN	RCLNLMP OFF
	TURN	LCLNLMP OFF
	IF	NOT BALLFLG INCR XBMTR
	IF	NOXBALL GO 20$
	TURN	XBLSW ON
	SOUND	10 EXSND
	GO	40$
20$:	IF	XBLOPT GO 30$
	SCORE	8 SPR50		;20K
	GO	40$
30$:	SCORE	12 SPR50	;30K
40$:	IF	BALLFLG GO FETEX
	SOUND	LSTSND
	IF	XBLFLG GO FETEX
	SET	CNT4 EXLMP
	TURN	LFFLG ON
	TURN	LMPSW ON
	DO	XBFLSH
	DO	XBFLSH
	DO	XBFLSH
	TURN	LFFLG OFF
	TURN	EXLNFLG OFF
	DELAY	200
	TURN	EXLNLMP OFF
	TURN	EX3LMP ON
	TURN	EX2LMP ON
	TURN	EX1LMP ON
	GO	FETEX
XBFLSH:	LAMPMODE 125
	LON	EX1LMP
	LOFF	EX2LMP
	LOFF	EX3LMP
	LOFF	EX4LMP,DEL
	LOFF	EX1LMP
	LON	EX2LMP,DEL
	LOFF	EX2LMP
	LON	EX3LMP,DEL
	LOFF	EX3LMP
	LON	EX4LMP,DEL,ENDLM
	DONE
SPFLASH:
	TURN	SPSLMP OFF
	DELAY	200
	IF	NOT EXBLKF DONE
	TURN	SPSLMP ON
	DELAY	200
	GO	SPFLASH
EXTRA2:	IF	EXBLKF DONE
	LOCKOUT	EXFLG
	IF	LNOPSW DONE
	IF	FTLNSW GO 10$
	TURN	LCLNLMP ON
	TURN	RCLNLMP ON
	DONE
10$:	IF	RCLNLMP DONE
	TURN	LCLNLMP ON
	DONE
;
;SPECIAL
;
SPECIAL:LOCKOUT	SPLKF
	TURN	SPCFLG OFF
	TURN	DLYFLG ON
	TURN	RLNLMP OFF
	TURN	LLNLMP OFF
	INCR	SPMTR
	IF	SPECALT GO SPALT
	IF	ADABALL GO SPL1
REPLAY:	SOUND	16 SPSND
	INCR	RPMTR
	TURN	RE1FLG ON	;GIVE REPLAY
	GO	SPEC1
SPL1:	TURN	BALLFLG ON
	DO	EXTRA
	TURN	BALLFLG OFF
	GO	SPEC1
SPALT:	IF	ADABALL GO 10$
	SCORE	10 SPSND1
	GO	SPEC1
10$:	SCORE	12 SPSND1
SPEC1:	SOUND	LSTSND
	SET	CNT4 SPLMP
	TURN	LFFLG ON
	TURN	LMPSW ON
	DO	SPFLSH
	DO	SPFLSH
	DO	SPFLSH
	TURN	LFFLG OFF
	TURN	SPLNFLG OFF
	DELAY	200
	TURN	SPLNLMP OFF
	TURN	SP1LMP ON
	TURN	SP2LMP ON
	TURN	SP3LMP ON
	TURN	SP4LMP ON
	GO	FETEX
SPFLSH:	LAMPMODE 100
	LON	SP1LMP
	LOFF	SP2LMP
	LOFF	SP3LMP
	LOFF	SP4LMP
	LOFF	SP5LMP,DEL
	LOFF	SP1LMP
	LON	SP2LMP,DEL
	LOFF	SP2LMP
	LON	SP3LMP,DEL
	LOFF	SP3LMP
	LON	SP4LMP,DEL
	LOFF	SP4LMP
	LON	SP5LMP,DEL,ENDLM
	DONE
;
;LAMP FLASH ROUTINE
;
LAMPFL:	TURN	WITH CNT4 OFF
	DELAY	80
	TURN	WITH CNT4 ON
	DELAY	80
	IF	LFFLG GO LAMPFL
	DONE
;CIRCLE DETECTOR ROLLOVER
RCIR:	LOCKOUT	CIRLKF
	DELAY	400
	TURN	CIRLKF OFF
	IF	CRLNLKF GO FLOOP
	SCORE 	5 HUND
	DONE
FLOOP:	SOUND	5 FLRSND	;FEATURE LOOP COMPLETED
	GO	FLPADV
;LEFT LANE ROLLOVER
LLN:	IF	LLNLMP TURN SPECSW ON
	WAIT	LSTSND
	SCORE	5 THOU
	GO 	BONADV
;LEFT CENTER ROLLOVER
LCLN:	IF	LCLNLMP TURN EXTRSW ON
	WAIT	LSTSND
	SCORE	3 THOU
	GO	BONADV
;RIGHT CENTER ROLLOVER
RCLN:	IF	RCLNLMP TURN EXTRSW ON
	WAIT	LSTSND
	SCORE	3 THOU
	GO	BONADV			;AND RETURN
;RIGHT LANE ROLLOVER
RLN:	IF	RLNLMP TURN SPECSW ON
	WAIT	LSTSND
	SCORE	5 THOU
	GO	BONADV
;
	.PAGE
	.SBTTL FEATURE LOOP ADVANCE
FLPADV:	IF	NOT LANLKF GO 5$
	INCR	CNT1
	DONE
5$:	LOCKOUT	LANLKF
6$:	DELAY	200
	IF	DLYFLG GO 6$
8$:	SET	CNT2 DB1LMP	;INC AND REMEMBER LAMPS
10$:	IF	NOT WITH CNT2 GO 20$
	INCR	CNT2		;IF LAMPS NOT STARTED, DON'T INC
	IF	NOT EQUAL CNT2 DBLMP GO 10$
20$:	SET	CNT3 EX1LMP
30$:	IF	NOT WITH CNT3 GO 40$
	INCR	CNT3
	IF	NOT EQUAL CNT3 EXLMP GO 30$
40$:	SET	CNT5 SP1LMP
50$:	IF	NOT WITH CNT5 GO 60$
	INCR	CNT5
	IF	NOT EQUAL CNT5 SPLMP GO 50$
60$:	TURN	FLFLG ON	;FEATURE LOOP FLASH
	SOUND	15 FLASND
	DO	ADFLSH
	DO	ADFLSH
	TURN	FLFLG OFF
	TURN	SPLMP OFF
	IF	EQUAL CNT2 DB1LMP GO 80$	;RESTORE INCREMENTED LAMPS
70$:	SCORE	THOU
	TURN	WITH CNT2 ON
	DECR	CNT2
	IF	NOT EQUAL CNT2 DB1LMP-1 GO 70$
80$:	IF	EQUAL CNT3 EX1LMP GO 100$
90$:	SCORE	THOU
	TURN	WITH CNT3 ON
	DECR	CNT3
	IF	NOT EQUAL CNT3 EX1LMP-1 GO 90$
100$:	IF	EQUAL CNT5 SP1LMP GO 120$
110$:	SCORE	THOU
	TURN	WITH CNT5 ON
	DECR	CNT5
	IF	NOT EQUAL CNT5 SP1LMP-1 GO 110$
120$:
	IF	NOT CNT1 GO FEATCK
	DECR	CNT1
	GO	8$
FEATCK:	TURN	LANLKF OFF
	IF	DB3LMP AND NOT DBLMP TURN DBFLG ON	;CHECK FOR DB,EXB,SPEC
	IF	DBLMP TURN DOBSW ON		;DOUBLE BONUS TASK
	IF	EX4LMP DO EXTRA2
	IF	EXLMP TURN EXTRSW ON
	IF	SP5LMP DO SPERN2
	IF	SPLMP TURN SPECSW ON
	DONE

	.SBTTL SPINNER
SPIN:	IF	SPINLMP GO 10$
	IF	SPIN2LMP GO 20$
	IF	SPIN1LMP GO 30$
	SCORE	SPN100
	DONE
10$:	SCORE	SPN1K
20$:	SCORE	SPN1K
30$:	SCORE	SPN1K
	DONE
SPINBACK:TURN	SPIN1LMP OFF
	TURN	SPIN2LMP OFF
	TURN	SPINLMP OFF
	DONE
	.PAGE
	.SBTTL 50 PT SWITCHES
R50:	SCORE	5 TEN
	DONE
	.PAGE
	.SBTTL TARGETS
;LEFT LOWER TARGET (40K)
LLTRG:	SCORE	THOU
	IF	B40LMP AND NOT B40FLG GO 10$
	GO	BONADV		;AND RETURN
10$:	TURN	B40FLG ON	;AND RETURN
	GO	TRGLOOP		;AND RETURN
;LEFT TARGET (20K)
LTRG:	SCORE	THOU
	IF	B20LMP AND NOT B20FLG GO 10$
	GO 	BONADV		;AND RETURN
10$:	TURN	B20FLG ON
	GO	TRGLOOP		;AND RETURN
;RIGHT TARGET (10K)
RTRG:	SCORE	THOU
	IF	B10LMP AND NOT B10FLG GO 10$
	GO	BONADV		;AND RETURN
10$:	TURN 	B10FLG ON
	GO	TRGLOOP		;AND RETURN
;RIGHT LOWER TARGET (30K)
RLTRG:	SCORE	THOU
	IF	B30LMP AND NOT B30FLG GO 10$
	GO	BONADV		;AND RETURN
10$:	TURN	B30FLG ON
;
TRGLOOP:DO	BONCOL
BONSET:	IF	B10FLG TURN B10LMP ON
	IF	B20FLG TURN B20LMP ON
	IF	B30FLG TURN B30LMP ON
	IF	B40FLG TURN B40LMP ON
	DONE
	.PAGE
	.SBTTL OUTHOLE SEQUENCE
;OUTHOLE KICKER
OHOL:	LOCKOUT	OUTFLG
	DELAY	240		;DEBOUNCE
	IF	NOT OHOLSW GO 100$	;BALL BOUNCED OUT
	IF	NOT TLT2FLG GO 10$
	TURN	XBLSW ON
	CLRNOT	SPSLMP,P1MIL,P2MIL,P3MIL,P4MIL,BALLMP,GMOVER
	CLRNOT	B10LMP,B20LMP,B30LMP,B40LMP
	TURN	DBFLG OFF
	TURN	EXFLG OFF
	TURN	SPCFLG OFF
	GO	15$		;FINISH BALL AFTER TILT
10$:	IF	NOT SCRFLG GO RESTART
	CLRALL	LTHEN,RTHEN,RSLEN,LSLEN
	SOUND	LSTSND
	WAIT	LSTSND
15$:	IF	B10LMP AND NOT B10FLG TURN BFFLG ON
	IF	B20LMP AND NOT B20FLG TURN BFFLG ON
	IF	B30LMP AND NOT B30FLG TURN BFFLG ON
	IF	B40LMP AND NOT B40FLG TURN BFFLG ON
	IF	NOT TLT2FLG DO BCOLOUT
	IF	B40FLG
	CLRALL	B40FLG B30FLG B20FLG B10FLG B1FLG B4FLG B3FLG B2FLG,B5FLG,B6FLG,B7FLG,B8FLG,B9FLG
20$:	IF	TLT2FLG AND BFFLG GO 50$
	DO	BONSET
	IF	NOT BFFLG GO 28$	;SET UP FOR FLASHING TARGET
	TURN	BFFLG OFF
	SET	CNT6 B10LMP
23$:	IF	NOT WITH CNT6 GO 26$
	INCR	CNT6
	GO	23$
26$:	TURN	WITH CNT6 ON
28$:	SET	CNT6 B1FLG
	SET	CNTR B1LMP
30$:	IF	NOT WITH CNT6 GO 50$
	TURN	WITH CNTR ON
	INCR	CNT6
	INCR	CNTR
	IF	NOT EQUAL CNT6 B9FLG+1 GO 30$
50$:	TURN	TLT2FLG OFF
	TURN	BFFLG OFF
	DELAY	50
	DO	FUNCLR
	IF	EXBLKF AND NOT NOXBALL GO RESTART	;XTRA BALL
	TURN	OUTFLG OFF
	NEXTPLAYER
100$:	TURN	OUTFLG OFF
	DONE
FUNCLR:	IF	FTOPSW AND DB3LMP GO 5$
	IF	NOT DBLMP GO 10$
5$:	CLRALL	DBLMP DB1LMP DB2LMP DB3LMP DBLNLMP
	TURN	DBFLG OFF
10$:	IF	FTOPSW AND EX4LMP GO 15$
	IF	NOT EXLMP GO 20$
15$:	CLRALL	EXLMP EX1LMP EX2LMP EX3LMP EX4LMP LCLNLMP RCLNLMP EXLNLMP
	TURN	EXFLG OFF
20$:	IF	FTOPSW AND SP5LMP GO 25$
	IF	NOT SPLMP GO 50$
25$:	CLRALL	SPLMP SP1LMP SP2LMP SP3LMP SP4LMP SP5LMP LLNLMP RLNLMP SPLNLMP
	TURN	SPCFLG OFF
50$:	IF	NOT LLNFSW DONE
	TURN	LLNLMP OFF
	TURN	LCLNLMP OFF
	TURN	RCLNLMP OFF
	TURN	RLNLMP OFF
	DONE
	.PAGE
	.SBTTL BONUS COLLECT
SETIT9:	SETALL	B1LMP,B2LMP,B3LMP,B4LMP,B5LMP,B6LMP,B7LMP,B8LMP,B9LMP
	DO	CLAIM2
BONCOL:
	SET	CNTR B9LMP
	TURN	BONFLG ON	;NO BONADV UNTIL DONE
10$:	IF	WITH CNTR DO CLAIM1
	DECR	CNTR
	IF	NOT EQUAL CNTR B1LMP-1 GO 10$
BC40:	IF	NOT B40LMP GO BC30	;CHECK 40K
	TURN	B40LMP OFF
	GO	SETIT9
BC30:	IF	NOT B30LMP GO BC20	;CHECK 30K
	TURN	B30LMP OFF
	GO	SETIT9
BC20:	IF	NOT B20LMP GO BC10	;CHECK 20K
	TURN	B20LMP OFF
	GO	SETIT9
BC10:	IF	NOT B10LMP TURN BONFLG OFF	;CHECK 10K
	IF	NOT B10LMP DONE
	TURN	B10LMP OFF
	GO	SETIT9
CLAIM1:	TURN	WITH CNTR OFF
CLAIM2:	IF	DBLMP AND OUTFLG SOUND BDRSND
BPAY1:	SCORE	BDRSND
BPAY2:	IF	OUTFLG WAIT BDRSND	;WAIT FOR SOUNDS
	IF	NOT OUTFLG DELAY 100
	DONE
;
;
;
BCOLOUT:IF	NOT BONFLG GO BONCOL	;VIRTUAL LOCKOUT
	DELAY	200
	GO	BCOLOUT
	.PAGE
	.SBTTL BONUS ADVANCE
BONADV:	IF	NOT BONFLG GO 10$
	DELAY	200
	GO	BONADV
10$:	IF	B40LMP AND B9LMP DONE
	IF	OUTFLG DONE
	IF	B10LMP AND NOT B10FLG DONE	;WAIT 10K TARG
	IF	B20LMP AND NOT B20FLG DONE	;WAIT 20K TARG
	IF	B30LMP AND NOT B30FLG DONE	;WAIT 30K TARG
	IF	B40LMP AND NOT B40FLG DONE	;WAIT 40K TARG
	IF 	NOT B9LMP GO BON1
	IF	B30LMP GO BON40
	IF	B20LMP GO BON30
	IF	B10LMP GO BON20
	GO	BON1
BON40:	TURN	B40LMP ON	;SET 40K LAMP
	GO	CLRIT9
BON30:	TURN	B30LMP ON	;SET 30K LAMP
	GO	CLRIT9
BON20:	TURN	B20LMP ON	;SET 20K LAMP
	GO	CLRIT9
BON1:	SET	CNTRA B1LMP
	SET	CNT6 B1FLG
10$:	IF	NOT WITH CNTRA GO 20$
	INCR	CNTRA
	INCR	CNT6
	IF	NOT EQUAL CNTRA B10LMP GO 10$
	TURN	WITH CNTRA ON	;SET 10K LAMP
	GO	CLRIT9		;CLR 1-9
20$:	TURN	WITH CNTRA ON
	TURN	WITH CNT6 ON
BONX:	SOUND	BONSND
	DONE
;
CLRIT9:	CLRALL	B1LMP,B2LMP,B3LMP,B4LMP,B5LMP,B6LMP,B7LMP,B8LMP,B9LMP
	CLRALL	B1FLG B2FLG B3FLG B4FLG B5FLG B6FLG B7FLG B8FLG B9FLG
	GO	BONX
	.PAGE
	.SBTTL FLASHING LIGHTS
FLASH:	IF	B10LMP AND NOT B10FLG TURN RTRGLMP ON
	IF	B20LMP AND NOT B20FLG TURN LTRGLMP ON
	IF	B30LMP AND NOT B30FLG TURN RLTRGLMP ON
	IF	B40LMP AND NOT B40FLG TURN LLTRGLMP ON
	IF	DBFLG TURN DOBLMP ON
	IF	EXFLG TURN XBLMP ON
	IF	SPCFLG TURN SPECLMP ON
	DELAY	600
	CLRALL	RTRGLMP,LTRGLMP,RLTRGLMP,LLTRGLMP
	CLRALL	DOBLMP,XBLMP,SPECLMP
	DELAY	300
	GO	FLASH
;
	.PAGE
	.SBTTL	RESTART
SETSOL:
RESTART:CLRALL	OUTFLG,SCRFLG,DOBLKF,LANLKF,SPLKF,BALLFLG,FLFLG
	CLRALL	CRLNLKF,CIRLKF,RSLFLG,LSLFLG,BONFLG,DLYFLG,BFFLG,TLTDFLG,TLT2FLG
	IF	 B10LMP GO 10$
	TURN	B1LMP ON
	TURN	B1FLG ON
10$:	SETALL	OUTHSOL,FLIPSOL,LTHEN,RTHEN,RSLEN,LSLEN
	DELAY	1000
	IF	OHOLSW GO 10$
	DONE
	.BLKB ^H0B000-.
	.END
                                                                                                                                                                                                                       